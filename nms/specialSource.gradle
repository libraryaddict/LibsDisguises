// Original code copied from https://github.com/Shopkeepers/Shopkeepers/tree/master

configurations {
    specialSource
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    specialSource 'net.md-5:SpecialSource:1.11.4:shaded'
}

ext.findArtifact = { group, module, version, classifier = '', ext = 'jar' ->
    def notation = "${group}:${module}:${version}"
    if (classifier) {
        notation += ":${classifier}"
    }
    notation += "@${ext}"

    def dependency = project.dependencies.create(notation)
    // Create a temporary configuration to resolve this single dependency
    def config = project.configurations.detachedConfiguration(dependency)
    config.setTransitive(false) // We only want this specific file, not its dependencies
    return config.singleFile
}

ext.remapMojangToObfuscated = { execOps, inputFile, outputFile, craftbukkitVersion ->
    def remappedMojang = findArtifact("org.spigotmc", "spigot", craftbukkitVersion, "remapped-mojang")
    def mojangMappings = findArtifact("org.spigotmc", "minecraft-server", craftbukkitVersion, "maps-mojang", "txt")

    println '> remapMojangToObfuscated'
    println '  Input: ' + inputFile.path
    println '  Output: ' + outputFile.path

    // Use the passed-in service, not project.javaexec
    execOps.javaexec {
        classpath = configurations.specialSource + files(remappedMojang)
        mainClass = 'net.md_5.specialsource.SpecialSource'
        args '--live', '-i', inputFile.path, '-o', outputFile.path, '-m', mojangMappings.path, '--reverse'
    }
}

// Converts from Minecraft's obfuscated mappings to Spigot's mappings.
ext.remapObfuscatedToSpigot = { execOps, inputFile, outputFile, craftbukkitVersion ->
    def spigotMappings = findArtifact("org.spigotmc", "minecraft-server", craftbukkitVersion, "maps-spigot", "csrg")
    def remappedObf = findArtifact("org.spigotmc", "spigot", craftbukkitVersion, "remapped-obf")

    println '> remapObfuscatedToSpigot'
    println '  Input: ' + inputFile.path
    println '  Output: ' + outputFile.path

    // Use the passed-in service, not project.javaexec
    execOps.javaexec {
        classpath = configurations.specialSource + files(remappedObf)
        mainClass = 'net.md_5.specialsource.SpecialSource'
        args '--live', '-i', inputFile.path, '-o', outputFile.path, '-m', spigotMappings.path
    }
}

// Converts from Mojang's mappings to Spigot's mappings.
ext.remapMojangToSpigot = { execOps, inputFile, intermediateFile, outputFile, craftbukkitVersion ->
    println '> remapMojangToSpigot'
    println '  Input: ' + inputFile.path
    println '  Intermediate: ' + intermediateFile.path
    println '  Output: ' + outputFile.path
    // Pass the execOps service down to the other functions
    remapMojangToObfuscated(execOps, inputFile, intermediateFile, craftbukkitVersion)
    remapObfuscatedToSpigot(execOps, intermediateFile, outputFile, craftbukkitVersion)
}