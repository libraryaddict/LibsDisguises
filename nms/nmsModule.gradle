// Code copied from https://github.com/Shopkeepers/Shopkeepers/tree/master

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

apply from: rootProject.file('nms/generator.gradle')
apply from: rootProject.file('nms/specialSource.gradle')

ext {
    // This needs to be defined by projects that apply this script.
    craftbukkitVersion = 'UNSPECIFIED'
    // This can be overridden by projects that apply this script.
    spigotServerCode = true
}

tasks.jar {
    exclude("*.properties")
}

generationConfig {
    templateDir = rootProject.project(':nms').layout.projectDirectory.dir('src/main/java-templates')
    defaultProperties = rootProject.project(':nms').layout.projectDirectory.file('src/main/resources/default.properties')
    overrideProperties = layout.projectDirectory.file('src/main/resources/override.properties')
}

dependencies {
    implementation project(':shared')
    compileOnly libs.io.netty.netty.buffer
    compileOnly libs.it.unimi.dsi.fastutil
    compileOnly libs.com.mojang.datafixerupper
    compileOnly libs.com.retro.packetevents
    afterEvaluate {
        if (spigotServerCode) {
            implementation(libs.org.spigotmc.spigot.api)
            implementation "org.spigotmc:spigot:${craftbukkitVersion}:remapped-mojang"
            implementation "org.spigotmc:spigot:${craftbukkitVersion}:remapped-obf"
            implementation "org.spigotmc:minecraft-server:${craftbukkitVersion}:maps-mojang@txt"
            implementation "org.spigotmc:minecraft-server:${craftbukkitVersion}:maps-spigot@csrg"
        } /*else {
            implementation "org.spigotmc:spigot:${craftbukkitVersion}"
        }*/
    }
}

afterEvaluate {
    if (spigotServerCode) {
        jar {
            doLast {
                // Get the ExecOperations service from the project
                def execOps = project.services.get(ExecOperations)

                def outputFiles = outputs.files.files
                for (inputFile in outputFiles) {
                    File intermediateFile = new File(temporaryDir, inputFile.name.replace('.jar', '-obf.jar'))
                    File outputFile = inputFile
                    // Call the function with the service as the first argument
                    project.ext.remapMojangToSpigot(execOps, inputFile, intermediateFile, outputFile, craftbukkitVersion)
                }
            }
        }
    }
}

// Note: The NMS modules are not published as standalone artifacts, but are shaded into the final plugin jar.
